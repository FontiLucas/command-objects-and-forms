Next, let's add the ability to update existing players. Add `edit` and `update` actions
to `PlayerController`. You should notice that these are almost the same as the `create`
and `save` actions.

[source,groovy]
----
    def edit(Player player) {
        respond player
    }

    def update(Player player) {
        if (player == null) {
            render status: HttpStatus.NOT_FOUND
            return
        }

        if (player.hasErrors()) {
            respond player.errors, view: 'edit'
            return
        }

        player.save flush: true

        request.withFormat {
            form multipartForm { redirect player }
            '*' { respond player, status: HttpStatus.OK }
        }
    }
----

The `edit` action starts with a `Player` command object; Grails fetches an existing `Player`
object automatically for you, and the properties of that object are loaded as the
initial values of the form in `edit.gsp`.

The `update` action is the same as `save`, except that errors redirect back to `edit`
rather than `create`, and the HTTP status for success is `OK` rather than `CREATED`.

Let's change the `update` action a bit. Assuming a player's win and loss counts will be
managed elsewhere, the wins and losses inputs have been removed from the form in `edit.gsp`.
While running the application, go to the player list and click on one of the
player names. While you can show the win/loss count, clicking the "Edit" button will
display a form that does not let you change the win/loss counts.

But this isn't sufficient to protect those properties. Just because the inputs have been
removed from the form *does not* prevent those fields from being changed. A clever user
can submit the form by other means that will change those values:

[source]
----
$ curl --request GET "http://localhost:8080/player/show.json?id=1"

{"id":1, "game":"Pandemic", "losses":30, "name":"Alexis Barnett", "region":"EAST", "wins":96}

$ curl --request POST --form "id=1" --form "wins=2" --form "losses=194" "http://localhost:8080/player/update.json"
----

After the second `curl` command, player Alexis Barnett will have only two wins and 194 losses,
rather than the 96 wins and 30 losses she should have.

One way to fix this would be to avoid the use of the `Player` command object and manage all
of the loading, binding, and verification manually. Then you could ignore any attempts to
modify the wins and losses properties.

But command objects do not require using a domain class: use any class! Define the following
class in `PlayerController.groovy` below the `PlayerController` class. Notice that the
property names of `PlayerInfo` match those of the form's `<input>` tags; submitted form values
will be bound to the command object's properties where the names match.

[source,groovy]
----
include::{sourceDir}/grails-app/controllers/demo/PlayerController.groovy[tags=playerInfo]
----

And change the `update` action to use `PlayerInfo` as the command object, using its content
to update the appropriate `Player` instance.

[source,groovy]
----
    def update(PlayerInfo info) {
        Player player = Player.get(params.id)
        if (player == null) {
            render status: HttpStatus.NOT_FOUND
            return
        }

        player.properties = info.properties
        player.save flush: true

        if (player.hasErrors()) {
            respond player.errors, view: 'edit'
            return
        }

        request.withFormat {
            form multipartForm { redirect player }
            '*' { respond player, status: HttpStatus.OK }
        }
    }
----

Since `PlayerInfo` does *not* contain `wins` or `losses`, those values will not be
accepted from the form submission, even if values are submitted for those names.

[source]
----
$ curl --request GET "http://localhost:8080/player/show.json?id=4"

{"id":4, "name":"Catherine Newton", "game":"Scythe", "region":"WEST", "wins":66, "losses":40}

$ curl --request POST --form "id=4" --form "name=June Smith" --form "game=Chess" --form "region=NORTH" --form "wins=0" --form "losses=10" "http://localhost:8080/player/update.json"

$ curl --request GET "http://localhost:8080/player/show.json?id=4"

{"id":4, "name":"June Smith", "game":"Chess", "region":"NORTH", "wins":66, "losses":40}
----

As you can see, the `name`, `game`, and `region` properties have all changed, while the
`wins` and `losses` properties have not changed despite our efforts to do so. By using the
`PlayerInfo` command object, we've restricted what fields are accepted from the form
submission while still maintaining all the benefits of command objects.

One thing this current solution is missing is validation. While `Player` will still be
validated when `player.save` is called, that now happens later in the action, after the
attempt to save. It would be better if the error check happened earlier, as it did in the
`save` action and the first version of `update`.

To resolve this, add a `constraints` block to `PlayerInfo`. When you defined the command
object class `PlayerInfo` in the `PlayerController.groovy` file, Grails recognized it as
a related command object class and made the class able to be validated. Change `PlayerInfo`
and add constraints.

[source,groovy]
----
include::{sourceDir}/grails-app/controllers/demo/PlayerController.groovy[tags=playerInfo;playerInfo-constraints]
----

Now revise the `update` action once more, putting error checking back near the top.

[source,groovy]
----
include::{sourceDir}/grails-app/controllers/demo/PlayerController.groovy[tags=update]
----
